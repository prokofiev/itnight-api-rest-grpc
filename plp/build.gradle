plugins {
    id 'org.springframework.boot' version '2.2.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
    id "io.freefair.lombok" version "4.1.3"
    id("org.openapi.generator") version '4.2.1'
    id("com.google.protobuf") version "0.8.8"
}

version = '1.0'
sourceCompatibility = '1.8'

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    implementation "org.openapitools:jackson-databind-nullable:0.2.0"
    implementation 'io.swagger:swagger-annotations:1.5.17'

    implementation "io.grpc:grpc-all:$grpcVersion"

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

openApiGenerate {
    generatorName = "java"
    library = "webclient"
    inputSpec = "${projectDir}/src/main/swagger/catalogue.json"
    outputDir = "${buildDir}/generated/source/rest/"
    apiPackage = "epam.itnight.catalogue.api"
    modelPackage = "epam.itnight.catalogue.model"
    configOptions = [
            dateLibrary: "java8",
            title      : "catalogue",
            useTags    : "true",
    ]
}

compileJava.dependsOn tasks.openApiGenerate

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs "${buildDir}/generated/source/proto/main/grpc"
            srcDirs "${buildDir}/generated/source/proto/main/java"
            srcDirs "${buildDir}/generated/source/rest/src/main/java"

        }
    }
}
